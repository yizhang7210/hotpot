buildscript {
    ext {
        lombok = 'org.projectlombok:lombok:1.18.6'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/yizhang7210/hotpot")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from(components["java"])
            artifactId = project.name
        }
    }
}

bintray {
    user = 'yizhang7210'
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true

    pkg {
        repo = 'hotpot'
        name = project.name
    }

}

repositories {
    jcenter()
}

java {
    sourceCompatibility = JavaVersion.VERSION_12
    targetCompatibility = JavaVersion.VERSION_12
    withSourcesJar()
    withJavadocJar()
}

jar {
    manifest {
        attributes(
            "Implementation-Title": project.name,
            "Implementation-Version": archiveVersion
        )
    }
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:2.2.4.RELEASE")
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.0"

    // Use the source version from the project, uncomment this line
    implementation project(":hotpot-core")

    // Use the published version, uncomment this line
    // implementation "com.hotpot:hotpot-core:${version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    // Lombok
    compileOnly lombok
    annotationProcessor lombok
    testCompileOnly lombok
    testAnnotationProcessor lombok
}

test {
    useJUnitPlatform()
}